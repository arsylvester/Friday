title: AbbyStart
tags: 
colorID: 0
position: 861,368
---
<<if visited("AbbyStart") is false>>
	<<objective("Find out Abby's Alibi", 
	"- Probably should take a look at where the body was. Maybe Abby knows more about it?", 
		"abby1_objective")>>
	<<set $Q1New to true>>
	<<set $Q3New to true>>
	<<set $Q4New to true>>
	<<tutorialprompt DialogueContainer>>
	<<set $new_question to true>>
	JACE: Afternoon, ma’am. I’m Detective Jace Acosta from the Melvern Police Department. You’re in my crime scene.
	ABBY: Correction. Your crime scene is my apartment. Or it was.
	ABBY: I’d offer you a drink, but I don’t really have access to my fridge at the moment. 
	JACE: It’s uh, no worries
	ABBY: Feel free to look around like the other cops. I’ll just be outside, you know, hovering. Ask me whatever you need.
	ABBY: Not that you asked, but I’m Abby by the way. Abby Adams.
<<else>>
	<<nextTutState DialogueContainer>>
	ABBY: What else do you need detective?
<<endif>>
[[AbbyMain]]
===

title: AbbyRepeat
tags: 
colorID: 0
position: 861,368
---
<<tutorialprompt DialogueContainer>>
JACE: Afternoon, ma’am. I’m Detective Jace Acosta from the Melvern Police Department. You’re in my crime scene.
ABBY: Correction. Your crime scene is my apartment. Or it was.
ABBY: I’d offer you a drink, but I don’t really have access to my fridge at the moment. 
JACE: It’s uh, no worries
ABBY: Feel free to look around like the other cops. I’ll just be outside, you know, hovering. Ask me whatever you need.
ABBY: Not that you asked, but I’m Abby by the way. Abby Adams.
[[AbbyMain]]
===

title: AbbyMain
tags: 
colorID: 0
position: 1163,357
---
<<if $visited_pennies and $broke_into and $visited_alone>>
	<<set $new_question to true>>
<<endif>>
[[Repeat conversation|AbbyRepeat]]
<<if $new_question is true>>
	[[<color=green>Ask Questions</color>|AbbyQuestions]]
	<<else>>
	[[Ask Questions|AbbyQuestions]]
<<endif>>
[[Interrogate|AbbyInterogation]]
<<tutorialprompt DialogueContainer>>
===

title: AbbyQuestions
tags: 
colorID: 0
position: 1163,357
---
<<set $new_question to false>>
<<if visited("AbbyQuestions") is true>>
	[[Was anyone else here when it happened?|AbbyQ2]]
	<<if $Q1New is true>>
		[[<color=green>What sort of relationship did you have with Friday?</color>|AbbyQ1]]
	<<else>>
		[[What sort of relationship did you have with Friday?|AbbyQ1]]
	<<endif>>
	<<if $broke_into is true>>
		<<if $Q3New is true>>
			[[<color=green>Broke into your apartment?</color>|AbbyQ3]]
		<<else>>
			[[Broke into your apartment?|AbbyQ3]]
		<<endif>>
	<<endif>>
	<<if $visited_pennies and $broke_into and $visited_alone>>
		<<if $Q4New is true>>
			[[<color=green>Where were you when it happened?</color>|AbbyWhere]]
		<<else>>
			[[Where were you when it happened?|AbbyWhere]]
		<<endif>>
	<<endif>>
	[[Back|AbbyMain]]
<<else>>
	[[<color=green>Was anyone else here when it happened?</color>|AbbyQ2]]
<<endif>>
===

title: AbbyInterogation
tags: 
colorID: 0
position: 1163,357
---
<<startquestioning Journal>>
<<tutorialprompt DialogueContainer>>
<<if $tryagain is false>>
	ABBY: What is it detective?
	<<tutorialprompt DialogueContainer>>
<<endif>>
->Confirm Interrogation Choices
	<<if question("old_tv", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyOldTV]]
    <<elseif question("gang_violence", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyGangs]]
    <<elseif question("bills", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyBills]]
    <<elseif question("differ_address", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyDiffAddress]]
    <<elseif question("manage_bills", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyManBills]]
    <<elseif question("pennies", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyPennies]]
    <<elseif question("chalk", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyChalk]]
	<<elseif question("locked_patio", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyLockedPatio]]
	<<elseif question("abby_work", "-") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyWork]]
    <<elseif question("friday_alone", "locked_door") is true>>
        <<stopquestioning Journal>>
		<<set $tryagain to false>>
		[[AbbyFridayWasAlone]]
	<<else>>
		ABBY: I'm sorry, what are you trying to get at?
		<<set $tryagain to true>>
		[[AbbyInterogation]]
	<<endif>>
->Quit Interrogation
	<<stopquestioning Journal>>
	<<set $tryagain to false>>
	[[AbbyMain]]
===

title: AbbyQ1
tags: 
colorID: 0
position: 1163,357
---
<<set $Q1New to false>>
ABBY: She was a... client of mine. Nothing personal. Walking in and finding her like that… It’s awful.
JACE: If it wasn’t personal, why was she in your home?
ABBY: She came in every Thursday like clockwork for her appoint- for our, uh, girl’s nights.
ABBY: ... She was getting real cozy here, though. She was always around the apartments, sniffing around. Wondered if her detective work followed her home one too many times.
JACE: Doesn’t sound like my quiet assistant.
ABBY: ‘Quiet assistant’? Ha!
ABBY: Don’t make me laugh. You barely knew her at all.
[[AbbyMain]]
===

title: AbbyQ2
tags: 
colorID: 0
position: 1163,357
---
ABBY: Gosh no. I’m not much for big groups. It was just me and Friday, like every Thursday.
<<startkey Journal friday_alone>>
JACE: So, Friday was alone?
<<tutorialprompt DialogueContainer>>
ABBY: That’s what I told the other cops! Friday was alone!
<<endkey Journal>>
JACE: That seems like it might be important to note down.
[[AbbyMain]]
===

title: AbbyQ3
tags: 
colorID: 0
position: 1163,357
---
<<set $Q3New to false>>
<<objectiveComplete Journal abby2_objective>>
<<objective("Find out Abby's Alibi", 
	"<S>- Probably should take a look at where the body was. Maybe Abby knows more about it?
- Did she admit someone broke into her apartment? Better press her on that one.</s>
- So she locked the door when she left AND Friday was alone? How could someone have gotten in then?", 
		"abby3_objective")>>
<<startkey Journal locked_door>>
ABBY: Yeah! I locked the door when I left, just like I always do.
<<endkey Journal>>
JACE: The door was open when you came back?
ABBY: Actually, no…
<<startkey Journal locked_door>>
ABBY: I had to unlock it again.
<<endkey Journal>>
ABBY: It’s a cheap door. I know I had to unlock it again, because you have to jimmy the lock sometimes, and if you jimmy it too hard the door practically flies open...
ABBY: I almost stepped right on top of her, because I was jimmying that lock...
JACE: Nothing was stolen?
ABBY: It… doesn’t appear that they took anything.
JACE: Sounds like somebody had a grudge, then.
ABBY: Were they after Friday, or were they after me, detective? Shouldn’t I be going into protective custody?
JACE: I… don’t think that will be necessary, ma’am.
JACE: We have no evidence to suggest that the killer... didn’t already get what they came for.
[[AbbyMain]]
===

title: AbbyOldTV
tags: 
colorID: 0
position: 1163,357
---
<<startkey Journal gang_violence>>
ABBY: Ugh, I don’t want to read that right now. It’ll just be more of the same. Constant, senseless gang-related violence.
ABBY: The weirdest part are the crimes themselves. A lot of assaults, random locals being accosted, but not a lot of muggings. Bill Finn said he’d clean it up when he got elected, but nothing’s changed. 
<<endkey Journal>>
ABBY: If they’re not after money, what are they after?
[[AbbyMain]]
===

title: AbbyGangs
tags: 
colorID: 0
position: 1163,357
---
JACE: I take it you’re familiar with the gangs in the area, then?
ABBY: I’m not <I>familiar</I> with them, Detective.
ABBY: Anybody with two brain cells to rub together knows better than to get involved with them.
ABBY: I’ve been lucky to stay out of their way the whole time I’ve lived here.
ABBY: Until now, anyway.
JACE: So you think this was caused by a local gang?
ABBY: Who else could it be?
ABBY: The governor needs to make good on his claims to get rid of them already.
ABBY: If anything, it seems like things have gotten worse since he was elected.
JACE: ...Trust me when I say our governor isn’t the kind of politician to really make an effort to reduce gang violence.
ABBY: ...
[[AbbyMain]]
===

title: AbbyBills
tags: 
colorID: 0
position: 1163,357
---
ABBY: Hey, now. I thought opening other people’s mail was against the law, Detective. Well, you haven’t actually opened anything, so I guess it’s fine.
[[AbbyMain]]
===

title: AbbyDiffAddress
tags: 
colorID: 0
position: 1163,357
---
JACE: Is there any reason you have mail from two different addresses here?
<<startkey Journal abby_work>>
ABBY: It’s not that uncommon in my line of work. Best to separate your personal and private lives, right?
<<endkey Journal>>
[[AbbyMain]]
===

title: AbbyManBills
tags: 
colorID: 0
position: 1163,357
---
JACE: You sure seem to have a lot of bills from the landlord.
ABBY: He sends them almost daily now. Persistent bugger.
<<startkey Journal raised_rent>>
ABBY: He raised the rent for all the tenants because of all the gang violence scaring people out. Total crap.
<<endkey Journal>>
JACE: I’m not sure that’s legal...
[[AbbyMain]]
===

title: AbbyPennies
tags: 
colorID: 0
position: 1163,357
---
<<tutorialprompt DialogueContainer>>
JACE: She was murdered with a gym sock full of… pennies. That mean anything to you?
ABBY: Why would it mean anything to me? It’s not my sock. I wouldn’t be caught dead in cheap hose like that.
JACE: You sure about that? 
ABBY: Alright, you got me. What’s so damn interesting about a gym sock full of pennies?
JACE: We just bagged a guy who killed two people by beating them to death with a stocking full of pennies - not a gym sock, but it’s close. 
JACE: Actually, I think the last time I saw Friday we were reviewing the case file one last time, just yesterday. 
ABBY: Oh my god, that’s horrible. 
ABBY: But are you sure you got the right guy? 
JACE: Oh yeah, he’s the right guy. Security footage nailed him. Just a weird coincidence, you know?
ABBY: What if it’s a copycat killer? I heard about that on some police show Friday made me watch.
JACE: Generally, copycats only copy big names. One rando in a small town who killed two people isn’t very inspiring - he can’t even be classified as a serial killer.
ABBY: Huh. The more you know. I wonder why the gym sock then?
//<<startkey Journal spare_money>>
//ABBY: Look around, Detective. This isn’t exactly the best place. If we had enough money to spare for crimes, we wouldn’t be living here.
//<<endkey Journal>>
<<set $visited_pennies to true>>
<<set $new_question to true>>
[[AbbyMain]]
===

title: AbbyChalk
tags: 
colorID: 0
position: 1163,357
---
<<objectiveComplete Journal abby1_objective>>
<<objective("Find out Abby's Alibi", 
	"<S>- Probably should take a look at where the body was. Maybe Abby knows more about it?</s>
- Did she admit someone broke into her apartment? Better press her on that one.", 
		"abby2_objective")>>
JACE: So you were the one who found her body, sometime around 7pm?
ABBY: Yep. It was some nasty stuff.
<<startkey Journal mail>>
ABBY: Found her right in the middle of the room. I left her by herself to go get my mail, and… yeah.
<<endkey Journal>>
ABBY: She was dead in my apartment.
ABBY: I don’t know if this makes me selfish detective, but...
ABBY: I can’t help but wonder. Friday is... gone, but they broke into my apartment to get to her. Was the killer trying to kill Friday, or trying to get to me?
<<set $broke_into to true>>
<<set $new_question to true>>
[[AbbyMain]]
===

title: AbbyLockedPatio
tags: 
colorID: 0
position: 1163,357
---
JACE: I’m guessing you don’t give out your key often?
ABBY: I don’t give out my key at all, Detective.
JACE: Haven’t dropped it or lost it for a little while?
ABBY: No. Listen, I’ll make it simple for you: the only other person with a key to give out is the landlord.
JACE: Was he here at the time of the murder?
ABBY: I don’t know what his schedule is like. I didn’t see him, but I don’t usually see him outside of his office, so that’s not weird.
[[AbbyMain]]
===

title: AbbyWork
tags: 
colorID: 0
position: 1163,357
---
ABBY: Don’t play coy. I’m an adult entertainer. Or, you know, an escort.
JACE: And Friday was your…?
ABBY: Client. We call them clients.
JACE: No, I get that. I just have a hard time picturing Friday like…
ABBY: You must not know her all that well, Jace.
[[AbbyMain]]
===

title: AbbyFridayWasAlone
tags: 
colorID: 0
position: 1163,357
---
<<objectiveComplete Journal abby3_objective>>
<<objective("Find out Abby's Alibi", 
	"<S>- Probably should take a look at where the body was. Maybe Abby knows more about it?
- Did she admit someone broke into her apartment? Better press her on that one.
- So she locked the door when she left AND Friday was alone? How could someone have gotten in then?</s>
- Then where was Abby?", 
		"abby4_objective")>>
JACE: Is there any other way into this room other than the front door? A fire escape, maybe?
<<startkey Journal locked_patio>>
ABBY: Huh? Well, we have patios, as you can see, but I keep mine locked up tight.
<<endkey Journal>>
ABBY: The last time I was out there, that airhead Tristan was watering her plants and rained some on me.
ABBY: A little water doesn’t hurt anyone, but it sure can make my mascara run.
JACE: I’m sure it does. But could someone enter through that patio door?
<<startkey Journal locked_patio>>
ABBY: They’d need a key if they wanted to enter from the back, but I suppose it’s possible.
<<endkey Journal>>
<<set $visited_alone to true>>
<<deduction("Friday was alone in a locked room when she was murdered.", "Friday was alone and the doors were locked. Altough its not impossible someone could got through the patio door with a key.", "friday_alone", "locked_door", "", "")>>
[[AbbyMain]]
===

title: AbbyWhere
tags: 
colorID: 0
position: 1163,357
---
<<objectiveComplete Journal abby4_objective>>
<<set $Q4New to false>>
ABBY: Where was I when…? Oh, I get it.
ABBY: She was killed in my apartment, so I had to have something to do with it. Riiight. I told you, I went to get my mail.
ABBY: Bernard could vouch for my innocence. We talked for a bit in the mailroom before I came back up. He was mad about something, but I can’t remember what...
JACE: Bernard is…?
ABBY: He lives in an apartment a couple stories up. Weird guy. His apartment is always noisy, and he’s always lugging around a case of beer - the bottled kind, not cans.
ABBY: But if you’re going to see other tenants, I’d advise you to go see Tristan. She has some… issues.
JACE: What kind of issues?
ABBY: Detective, I thought Friday was your partner.
ABBY: … Tristan was Friday’s beau. Emphasis on was.
//ABBY: Actually, let me go with you if you’re going to visit Bernard. Something tells me you’ll get lost without me.
//ABBY: Besides, I need to get out of here. I can still see her body lying there.
//ABBY: After you, Detective.
<<openlocation LevelController Tristan_Room>>
<<openlocation LevelController Bernard_Room>>
<<openlocation LevelController Management_Office>>
<<openlocation LevelController Owner_Suite_Test>>
<<objective("Talk to Bernard or Tristan", 
	"I now should talk to either Bernard or Tristan.", 
		"Bernard_objective")>>
===