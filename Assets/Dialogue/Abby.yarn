title: AbbyStart
tags: 
colorID: 0
position: 861,368
---
<<if visited("AbbyStart") is false>>
JACE: Let’s get this over with...
JACE: Afternoon, ma,am. I’m Detective Jace Acosta from the Melvern Police Department. You’re in my crime scene.
ABBY: Correction. Your crime scene is my apartment. Or it was.
ABBY: Come in, come in.
ABBY: I’d offer you a drink, but I’d rather just get this over with... You’re here about Friday, aren’t you?
ABBY: I need to pack my things, so feel free to look around like the other cops. Ask me whatever you need.
<<else>>
ABBY: What else do you need detective?
<<endif>>
[[AbbyMain]]
===

title: AbbyRepeat
tags: 
colorID: 0
position: 861,368
---
JACE: Let’s get this over with...
JACE: Afternoon, ma,am. I’m Detective Jace Acosta from the Melvern Police Department. You’re in my crime scene.
ABBY: Correction. Your crime scene is my apartment. Or it was.
ABBY: Come in, come in.
ABBY: I’d offer you a drink, but I’d rather just get this over with... You’re here about Friday, aren’t you?
ABBY: I need to pack my things, so feel free to look around like the other cops. Ask me whatever you need.
[[AbbyMain]]
===

title: AbbyMain
tags: 
colorID: 0
position: 1163,357
---
[[Repeat conversation|AbbyRepeat]]
[[What sort of relationship did you have with Friday?|AbbyQ1]]
[[Was anyone else here when it happened?|AbbyQ2]]
<<if $broke_into is true>>
    [[Broke into your apartment?|AbbyQ3]]
<<endif>>
<<if $visited_pennies and $broke_into and $visited_alone>>
    [[Where were you when it happened?|AbbyWhere]]
<<endif>>
[[Interogate|AbbyInterogation]]
===

title: AbbyInterogation
tags: 
colorID: 0
position: 1163,357
---
<<startquestioning Journal>>
<<if $tryagain is false>>
	ABBY: What is it detective?
<<endif>>
->Confirm Questioning Choices
	<<if question("old_tv", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyOldTV]]
    <<elseif question("gang_violence", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyGangs]]
    <<elseif question("bills", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyBills]]
    <<elseif question("differ_address", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyDiffAddress]]
    <<elseif question("manage_bills", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyManBills]]
    <<elseif question("pennies", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyPennies]]
    <<elseif question("chalk", "-") is true>>
        <<stopquestioning Journal>>
		[[AbbyChalk]]
    <<elseif question("friday_alone", "locked_door") is true>>
        <<stopquestioning Journal>>
		[[AbbyFridayWasAlone]]
	<<else>>
		ABBY: I'm sorry, what are you trying to get at?
		<<set $tryagain to true>>
		[[AbbyInterogation]]
	<<endif>>
->Quit Questioning
	<<stopquestioning Journal>>
	<<set $tryagain to false>>
	[[AbbyMain]]
===

title: AbbyQ1
tags: 
colorID: 0
position: 1163,357
---
ABBY: She was a... client of mine. Nothing personal. Walking in and finding her like that… It’s awful.
JACE: If it wasn’t personal, why was she in your home?
ABBY: She was here for an appointment. Every Thursday like clockwork.
ABBY: She was getting real cozy here, though. She was always around the apartments. Almost too clingy for me.
JACE: Friday being clingy? Doesn’t sound like my quiet assistant.
ABBY: You, however, sound exactly like the inattentive dolt she described you as.
JACE: Inattentive…?
JACE: ( Dolt…? I was the one who solved all of our cases! )
[[AbbyMain]]
===

title: AbbyQ2
tags: 
colorID: 0
position: 1163,357
---
ABBY: Gosh no. I don’t enjoy having company over often.
ABBY: And I’d never have someone else over while handling my appointments.
<<startkey Journal friday_alone>>
JACE: So, Friday was alone?
ABBY: That’s what I’m saying! Friday was alone!
<<endkey Journal>>
JACE: Just making sure...
[[AbbyMain]]
===

title: AbbyQ3
tags: 
colorID: 0
position: 1163,357
---
<<startkey Journal locked_door>>
ABBY: Yeah! I locked the door when I left, just like I always do.
<<endkey Journal>>
JACE: The door was open when you came back?
ABBY: Actually, no…
<<startkey Journal locked_door>>
ABBY: I had to unlock it again.
<<endkey Journal>>
ABBY: Or I went through the motion, anyway. It may have been unlocked.
ABBY: The door was closed, for sure.
JACE: Nothing was stolen?
ABBY: It… doesn’t appear that they took anything.
JACE: Sounds like somebody had a grudge, then.
ABBY: I-I told you! They were after me, Detective…! Should I be put into protective custody or something?
JACE: I… don’t think that will be necessary, ma’am.
[[AbbyMain]]
===

title: AbbyOldTV
tags: 
colorID: 0
position: 1163,357
---
NEWSCASTER: A young cop was murdered at the Sunnyview Apartment complex last night. 
<<startkey Journal gang_violence>>
NEWCASTER: Initial reports seem to connect the incident to a string of gang violence common in the area.
ABBY:  Shame that the neighborhood has gotten so bad lately. It all seems so senseless.
<<endkey Journal>>
[[AbbyMain]]
===

title: AbbyGangs
tags: 
colorID: 0
position: 1163,357
---
JACE: I take it you’re familiar with the gangs in the area, then?
ABBY: I’m not familiar with them, Detective.
ABBY: But it’s hard to live here without having heard of the muggings, stabbings, and shoot outs. I’m lucky I’ve never been involved in anything.
ABBY: Until now, anyway.
JACE: So you think this was caused by a local gang?
ABBY: Who else could it be?
ABBY: The governor needs to make good on his claims to get rid of them already.
ABBY: As long as it stays like this, it might  be me who they find dead on the floor next…!
JACE: Let’s not jump to conclusions, now…
[[AbbyMain]]
===

title: AbbyBills
tags: 
colorID: 0
position: 1163,357
---
ABBY: Hey, now. I thought opening other people’s mail was against the law, Detective. Well, you haven’t actually opened anything, so I guess it’s fine.
[[AbbyMain]]
===

title: AbbyDiffAddress
tags: 
colorID: 0
position: 1163,357
---
JACE: Is there any reason you have mail from two different addresses here?
<<startkey Journal abby_work>>
ABBY: It’s not that uncommon in my line of work. Best to separate your personal and private lives, right?
<<endkey Journal>>
[[AbbyMain]]
===

title: AbbyManBills
tags: 
colorID: 0
position: 1163,357
---
JACE: You sure seem to have a lot of bills from the landlord.
ABBY: He sends them almost daily now. Persistent bugger.
<<startkey Journal raised_rent>>
ABBY: He raised the rent for all the tenants because of all the gang violence scaring people out. Total crap.
<<endkey Journal>>
JACE: ( I don’t think that’s legal… )
[[AbbyMain]]
===

title: AbbyPennies
tags: 
colorID: 0
position: 1163,357
---
JACE: She was murdered with a stocking full of… pennies. That mean anything to you?
ABBY: Why would it mean anything to me? It’s not my stocking. I wouldn’t be caught dead in cheap hose like that.
JACE: I’m asking if anything else in the neighborhood sounds similar? Pennies used in other incidents?
<<startkey Journal spare_money>>
ABBY: Look around, Detective. This isn’t exactly the best place. If we had enough money to spare for crimes, we wouldn’t be living here.
<<endkey Journal>>
JACE: So you have no idea why it was shoved in Friday’s mouth, then?
ABBY: I certainly didn’t put it there, if that’s what you mean.
<<set $visited_pennies to true>>
[[AbbyMain]]
===

title: AbbyChalk
tags: 
colorID: 0
position: 1163,357
---
JACE: So you were the one who found her body?
ABBY: Yep. It was some nasty stuff.
<<startkey Journal mail>>
ABBY: Found her right in the middle of the room. I left her by herself to go get my mail, and… yeah.
<<endkey Journal>>
ABBY: She was dead in my apartment.
ABBY: Someone broke into my apartment, Detective. Doesn’t that mean they had to be after me?
<<set $broke_into to true>>
[[AbbyMain]]
===

title: AbbyFridayWasAlone
tags: 
colorID: 0
position: 1163,357
---
JACE: Is there any other way into this room other than the front door? A fire escape, maybe?
<<startkey Journal locked_patio>>
ABBY: Huh? Well, we have patios, as you can see, but I keep mine locked up tight.
<<endkey Journal>>
ABBY: The last time I was out there, that airhead Tristan was watering her plants and rained some on me.
ABBY: A little water doesn’t hurt anyone, but it sure can make my mascara run.
JACE: I’m sure it does. But could someone enter through that patio door?
<<startkey>>
ABBY: They’d need a key if they wanted to enter from the back, but I suppose it’s possible.
<<endkey>>
<<set $visited_alone to true>>
<<deduction("Friday was alone in a locked room when she was murdered.", "Friday was alone and the doors were locked. Altough its not impossible someone could got through the patio with a key.", "friday_alone", "locked_door", "", "")>>
[[AbbyMain]]
===

title: AbbyWhere
tags: 
colorID: 0
position: 1163,357
---
ABBY: Where was I when…? Oh, I get it.
ABBY: She was killed in my apartment, so I had to have something to do with it. Riiight. I told you, I went to get my mail.
ABBY: Bernard could vouch for my innocence. He saw me when I was coming back up.
JACE: Bernard is…?
ABBY: He lives in an apartment above this one. Weird guy. Always making a ton of racket.
ABBY: But if you’re going to see other tenants, I’d advise you to go see Tristan. She has some… issues.
JACE: Why would she have any problems?
ABBY: Detective, I thought Friday was your partner.
ABBY: … Tristan was Friday’s beau. Emphasis on was.
ABBY: Actually, let me go with you if you’re going to visit Bernard. Something tells me you’ll get lost without me.
ABBY: Besides, I need to get out of here. I can still see her body lying there.
ABBY: After you, Detective.
<<openlocation LevelController Tristan_Room>>
<<openlocation LevelController Bernard_Room>>
<<openlocation LevelController Management_Office>>
<<openlocation LevelController Owner_Suite_Test>>
*You may now access other rooms*
===